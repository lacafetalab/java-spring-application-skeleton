// Common for all projects
allprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'org.springframework.boot'

    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        mavenCentral()
    }

    dependencies {
        // Prod
        implementation 'org.apache.logging.log4j:log4j-core:2.12.1'
        implementation 'com.vlkan.log4j2:log4j2-logstash-layout:0.21'

		compileOnly 'org.projectlombok:lombok:1.18.8'
		annotationProcessor 'org.projectlombok:lombok:1.18.8'

		compile 'com.google.code.gson:gson:2.8.5'
		
		compile 'javax.persistence:javax.persistence-api:2.2'
		compile 'javax.transaction:javax.transaction-api:1.3'
		compile 'com.vladmihalcea:hibernate-types-52:2.5.0'
		compile 'org.hibernate:hibernate-core:5.4.4.Final'

        // Test
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
        testImplementation 'org.mockito:mockito-core:2.+'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
    }

    test {
        useJUnitPlatform()

        testLogging {
            events "passed", "skipped", "failed"
        }
    }
    
    sourceSets {
        main {
            java { srcDirs = [ 'main' ] }
            resources { srcDir 'resources' }
        }
        test {
            java { srcDirs = [ 'test' ] }
        }
    }

    task view_paths {
        doLast { task ->
            println "$task.project.name"
            println "------------------"
            println "Main: $sourceSets.main.java.srcDirTrees"
            println "   Resources: $sourceSets.main.resources.srcDirTrees"
            println "Tests: $sourceSets.test.java.srcDirTrees"
            println "   Resources: $sourceSets.test.resources.srcDirTrees"
        }
    }
}

// All subprojects (located in src/*)
subprojects {
    group = "pe.lacafetalab.application.${rootProject.name}"

    

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web:2.1.8.RELEASE'

        testImplementation rootProject.sourceSets.main.output
        testImplementation 'org.springframework.boot:spring-boot-starter-test:2.2.1.RELEASE'

        if (project.name != "shared") {
            implementation project(":shared")
            testImplementation project(":shared").sourceSets.test.output
        }
    }

    bootJar {
        enabled = false
    }

    jar {
        enabled = true
    }
}

// Main project (located in apps/)
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath('org.springframework.boot:spring-boot-gradle-plugin:2.2.1.RELEASE')
    }
}

apply plugin: 'application'

bootJar {
    baseName = 'java-ddd-skeleton'
    version = '0.0.1'
}

mainClassName = 'pe.lacafetalab.application.Starter'

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web:2.1.8.RELEASE'

    implementation project(":shared")
    implementation project(":manage")

    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.1.8.RELEASE'
    testImplementation project(":shared").sourceSets.test.output
}

